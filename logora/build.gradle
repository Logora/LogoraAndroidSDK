buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace "com.logora.logora_sdk"
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Logora/LogoraAndroidSDK")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.logora'
                artifactId = 'logora_sdk'
                version = System.getenv("RELEASE_TAG") ?: '0.1.0'

                pom {
                    name = 'Logora SDK'
                    description = 'Android SDK for the Logora debate space'
                    url = 'https://github.com/Logora/LogoraAndroidSDK'
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.fragment:fragment:1.6.1'
    implementation "androidx.recyclerview:recyclerview:1.3.1"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.test:monitor:1.6.1'
    implementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'junit:junit:4.13.2'
}
